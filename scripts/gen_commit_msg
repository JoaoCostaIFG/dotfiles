#!/bin/fish
### Uses python-llm to generate commit messages.
### First add/stage the changes you want to generate the commit for.
### It generates 3 commit message and then you can choose the one you want.
### Depends on python-llm, git, and jq.

function gen_commit_msg --argument context
    set -l use_emoji no
    set -l attempts 3

    function print_help -a msg -V script_name
        if test -n "$msg"
            set_color red
            echo "$msg"
        end
        set_color yellow
        echo "Usage:"
        echo "  `git add` the files to the staging area"
        echo "  then run the script, optionally passing in a context string."
    end

    set gitDiff (git --no-pager diff --cached | string collect)
    if test -z "$gitDiff"
        print_help "The diff is empty."
        return 1
    end

    if test -n "$context"
        set context "Take into account the following context: $context."
    end

    set codeblocked_git_diff "```
$gitDiff```"
    set identity "You are to act as an author of a commit message in git."

    if test "$use_emoji" = yes
        set commit_preface "Use GitMoji convention to preface the commit. Here are some help to choose the right emoji (emoji, description):
üêõ, Fix a bug;
‚ú®, Introduce new features;
üìù, Add or update documentation;
üöÄ, Deploy stuff;
‚úÖ, Add, update, or pass tests;
‚ôªÔ∏è, Refactor code;
‚¨ÜÔ∏è, Upgrade dependencies;
üîß, Add or update configuration files;
üåê, Internationalization and localization;
üí°, Add or update comments in source code;"
    else
        set commit_preface "Do not preface the commit with anything, except for the conventional commit keywords (in lower case): fix, feat, build, chore, ci, docs, style, refactor, perf, test."
    end

    set prompt "$identity Your mission is to create clean and comprehensive commit messages as per the Conventional Commit Convention and explain WHAT were the changes and mainly WHY the changes were done.
I'll send you an output of 'git diff --staged' command, and you are to convert it into a commit message.
$commit_preface
Don\'t start it with 'This commit', just describe the changes.
Use the present tense. Lines must not be longer than 74 characters. Use english for the commit message. Generate $attempts different examples for the commit message.
Place the commit message text inside markdown code blocks (```). The output shall follow the following JSON format:

```json
{
  "description": "General explanation of the commit message",
  "commits": [
    {
      "commit": "The commit message",
    },
  ]
}
```

Only include the JSON output in your answer.

$context

```
$gitDiff
```"

    set json (echo $prompt | ask | sed '1d;$d' | string collect)

    set -l description (echo $json | jq -r '.description' | string collect)
    set -l commit1 (echo $json | jq -r '.commits.[0].commit' | string collect)
    set -l commit2 (echo $json | jq -r '.commits.[1].commit' | string collect)
    set -l commit3 (echo $json | jq -r '.commits.[2].commit' | string collect)

    echo $description
    echo
    echo "--- 1 ----"
    echo $commit1
    echo "--- 2 ----"
    echo $commit2
    echo "--- 3 ----"
    echo $commit3

    read -l choices

    set answer ""

    if string match -e 1 $choices
        set answer $answer\n\n$commit1
    end
    if string match -e 2 $choices
        set answer $answer\n\n$commit2
    end
    if string match -e 3 $choices
        set answer $answer\n\n$commit3
    end

    if test -z $answer
        print_help "No commits chosen to take to the editor."
        return 1
    end

    git commit -s -e -m "$answer"
end

gen_commit_msg "$argv"
