#!/bin/fish
### Uses python-llm to generate commit messages.
### First add/stage the changes you want to generate the commit for.
### It generates 3 commit message and then you can choose the one you want.
### Depends on python-llm, git, and jq.

function gen_commit_msg --argument context
    set -l use_emoji no
    set -l attempts 3

    function print_help -a msg -V script_name
        if test -n "$msg"
            set_color red
            echo "$msg"
        end
        set_color yellow
        echo "Usage:"
        echo "  `git add` the files to the staging area"
        echo "  then run the script, optionally passing in a context string."
    end

    set gitDiff (git --no-pager diff --cached | string collect)
    if test -z "$gitDiff"
        print_help "The diff is empty."
        return 1
    end

    set gitHistory (git log -n10 --pretty=format:"Commit: %h\nSubject: %s\nBody:\n%b\n---" | string collect)

    if test -n "$context"
        set context "Take into account the following context: $context."
    end

    set identity "You are to act as an author of a git commit message."

    set prompt "$identity
- Create commit messages as per the Conventional Commit Convention:
```
<type>[optional scope]: <description>

<body>

[optional footer(s)]
```
- Explain WHY changes were made instead on WHAT changed. You'll have access to the 'git diff' of the staged files and the last 10 commit.
- Use imperative form/present tense.
- Don't use constructs like 'This commit'.
- The commit subject must be at most 50 characters long.
- The lines of the commit body must not be longer than 72 characters.
- Use english for the commit message.
- Always include a body.
- Generate $attempts different for the commit messages.
- Your output shall follow the following JSON format (only include this output in your response):

<output_format>
{
  "description": "General explanation of the git diff",
  "commits": [
    {
      "commit": "The commit message",
    },
  ]
}
</output_format>

$context

<git_diff>
$gitDiff
</git_diff>

<git_history>
$gitHistory
</git_history>
"

    set json (echo $prompt | ask | sed '1d;$d' | string collect)

    set -l description (echo $json | jq -r '.description' | string collect)
    set -l commit1 (echo $json | jq -r '.commits.[0].commit' | string collect)
    set -l commit2 (echo $json | jq -r '.commits.[1].commit' | string collect)
    set -l commit3 (echo $json | jq -r '.commits.[2].commit' | string collect)

    echo $description
    echo
    echo "--- 1 ----"
    echo $commit1
    echo "--- 2 ----"
    echo $commit2
    echo "--- 3 ----"
    echo $commit3

    read -l choices

    set answer ""

    if string match -e 1 $choices
        set answer $answer\n\n$commit1
    end
    if string match -e 2 $choices
        set answer $answer\n\n$commit2
    end
    if string match -e 3 $choices
        set answer $answer\n\n$commit3
    end

    if test -z $answer
        print_help "No commits chosen to take to the editor."
        return 1
    end

    git commit -s -e -m "$answer"
end

gen_commit_msg "$argv"
